#version 450

layout(std140, set = 1, binding = 0) uniform LightWorldPositionUniform {
    vec3 lightWorldPos;
} lightWorldPositionUniform;

layout(std140, set = 3, binding = 0) uniform ViewUniforms {
	mat4 viewProjection;
	mat4 viewInverse;
    mat4 world;
	mat4 worldInverseTranspose;
    mat4 worldViewProjection;
} viewUniforms;

layout(std140, set = 2, binding = 0) uniform FishVertexUniforms {
    float fishLength;
    float fishWaveLength;
    float fishBendAmount;
 } fishVertexUnifoms;
 
layout(std140, set = 3, binding = 1) uniform FishPerUniforms {
    vec3 worldPosition;
	float padding1;
    vec3 nextPosition;
	float padding2;
    float scale;
    float time;
} fishPerUniform;

layout(location = 0) in vec4 position;
layout(location = 1) in vec3 normal;
layout(location = 2) in vec2 texCoord;
layout(location = 3) in vec3 tangent;  // #normalMap
layout(location = 4) in vec3 binormal;  // #normalMap
layout(location = 0) out vec4 v_position;
layout(location = 1) out vec2 v_texCoord;
layout(location = 2) out vec3 v_tangent;  // #normalMap
layout(location = 3) out vec3 v_binormal;  // #normalMap
layout(location = 4) out vec3 v_normal;
layout(location = 5) out vec3 v_surfaceToLight;
layout(location = 6) out vec3 v_surfaceToView;
void main() {
  vec3 vz = normalize(fishPerUniform.worldPosition - fishPerUniform.nextPosition);
  vec3 vx = normalize(cross(vec3(0,1,0), vz));
  vec3 vy = cross(vz, vx);
  mat4 orientMat = mat4(
    vec4(vx, 0),
    vec4(vy, 0),
    vec4(vz, 0),
    vec4(fishPerUniform.worldPosition, 1));
  mat4 scaleMat = mat4(
    vec4(fishPerUniform.scale, 0, 0, 0),
    vec4(0, fishPerUniform.scale, 0, 0),
    vec4(0, 0, fishPerUniform.scale, 0),
    vec4(0, 0, 0, 1));
  mat4 world = orientMat * scaleMat;
  mat4 worldViewProjection = viewUniforms.viewProjection * world;
  mat4 worldInverseTranspose = world;

  v_texCoord = texCoord;
  // NOTE:If you change this you need to change the laser code to match!
  float mult = position.z > 0.0 ?
      (position.z / fishVertexUnifoms.fishLength) :
      (-position.z / fishVertexUnifoms.fishLength * 2.0);
  float s = sin(fishPerUniform.time + mult * fishVertexUnifoms.fishWaveLength);
  float a = sign(s);
  float offset = pow(mult, 2.0) * s * fishVertexUnifoms.fishBendAmount;
  v_position = (
      worldViewProjection *
      (position +
       vec4(offset, 0, 0, 0)));
  v_normal = (worldInverseTranspose * vec4(normal, 0)).xyz;
  v_surfaceToLight = lightWorldPositionUniform.lightWorldPos - (world * position).xyz;
  v_surfaceToView = (viewUniforms.viewInverse[3] - (world * position)).xyz;
  v_binormal = (worldInverseTranspose * vec4(binormal, 0)).xyz;  // #normalMap
  v_tangent = (worldInverseTranspose * vec4(tangent, 0)).xyz;  // #normalMap
  v_position.y = -v_position.y;
  gl_Position = v_position;
}